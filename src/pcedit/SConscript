
Import("env platform common_sources party_classes install_dir")

pced_sources = Split("""
	pc.action.cpp
	pc.editors.cpp
	pc.fileio.cpp
	pc.graphics.cpp
	pc.main.cpp
	../tools/fileio_party.cpp
""")

if str(platform) == "darwin":
	pced_sources.extend(Split("""
		pc.appleevents.mm
		pc.menus.mac.mm
	"""))
elif str(platform) == "win32":
	pced_sources.extend(Split("""
		pc.menus.win.cpp
	"""))
	pced_sources.append(env.RES('#rsrc/menus/CharEditor.rc'))

pced = env.Program("#build/bin/BoE Character Editor", pced_sources + common_sources + party_classes)

if str(platform) == "darwin":
	pced_info = {
		'nib': '#rsrc/menus/pcedit',
		'plist': 'BoECharEd-Info.plist',
		'creator': 'blxe',
		'icons': 'BoECharEd',
	}
else:
	pced_info = {}

env.Package(pced, install_dir, pced_info)
