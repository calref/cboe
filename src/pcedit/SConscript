
Import("env platform common_sources party_classes install_dir")

pced_sources = Split("""
	pc.action.cpp
	pc.editors.cpp
	pc.fileio.cpp
	pc.graphics.cpp
	pc.main.cpp
	../fileio/fileio_party.cpp
	../deps/fmtlib/src/format.cc
	../deps/fmtlib/src/os.cc
""")

if str(platform) == "darwin":
	pced_sources.extend(Split("""
		pc.appleevents.mm
		pc.menus.mac.mm
	"""))
elif str(platform) == "win32":
	pced_sources.extend(Split("""
		pc.menus.win.cpp
	"""))
	pced_sources.append(env.RES('#rsrc/menus/CharEditor.rc'))
elif str(platform) == "posix":
	pced_sources.extend(Split("""
		pc.menus.linux.cpp
		pc.menu.cpp
	"""))

pced = env.Program("#build/bin/BoE Character Editor", pced_sources + party_classes + common_sources)
debug_symbols = None
if str(platform) == "win32" and 'msvc' in env["TOOLS"] and not env['release']:
	debug_symbols = pced[0].abspath.replace('.exe', '.pdb')

if str(platform) == "darwin":
	pced_info = {
		'nib': '#rsrc/menus/pcedit',
		'plist': '#pkg/mac/BoECharEd-Info.plist',
		'creator': 'blxe',
		'icons': 'BoECharEd',
	}
else:
	pced_info = {}

env.Package(pced, install_dir, pced_info)
if debug_symbols is not None:
	env.Install(install_dir, debug_symbols)