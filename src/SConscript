
Import("env platform common_sources party_classes install_dir")

game_sources = Split("""
	boe.actions.cpp
	boe.combat.cpp
	boe.dlgutil.cpp
	boe.fileio.cpp
	boe.graphics.cpp
	boe.graphutil.cpp
	boe.infodlg.cpp
	boe.items.cpp
	boe.locutils.cpp
	boe.main.cpp
	boe.monster.cpp
	boe.newgraph.cpp
	boe.party.cpp
	boe.specials.cpp
	boe.startup.cpp
	boe.text.cpp
	boe.town.cpp
	boe.townspec.cpp
	pcedit/pc.editors.cpp
	tools/fileio_party.cpp
""")

if str(platform) == "darwin":
	game_sources.extend(Split("""
		boe.appleevents.mm
		boe.menus.mac.mm
	"""))
elif str(platform) == "win32":
	game_sources.extend(Split("""
		boe.menus.win.cpp
	"""))
	game_sources.append(env.RES('#build/obj/BladesOfExile.res', '#rsrc/menus/BladesOfExile.rc'))

boe = env.Program("#build/bin/Blades of Exile", game_sources + common_sources + party_classes)

if str(platform) == "darwin":
	boe_info = {
		'nib': '#rsrc/menus/game',
		'plist': 'BoE-Info.plist',
		'creator': 'blx!',
		'icons': 'BoE boegraphics boeresources boesave boesounds',
	}
else:
	boe_info = {}

env.Package(boe, install_dir, boe_info)
